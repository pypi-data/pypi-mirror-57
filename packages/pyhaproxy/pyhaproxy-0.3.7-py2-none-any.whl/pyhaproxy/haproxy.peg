grammar HaproxyConfig

configuration       <-      (comment_line / blank_line / global_section / defaults_section / userlist_section / listen_section / frontend_section / backend_section)*

global_section      <-      global_header config_block

defaults_section    <-      defaults_header config_block

userlist_section    <-      userlist_header config_block

listen_section      <-      listen_header config_block

frontend_section    <-      frontend_header config_block

backend_section     <-      backend_header config_block

global_header       <-      whitespace "global" whitespace comment_text? line_break

userlist_header     <-      whitespace "userlist" whitespace proxy_name comment_text? line_break

defaults_header     <-      whitespace "defaults" whitespace proxy_name? whitespace comment_text? line_break

listen_header       <-      whitespace "listen" whitespace proxy_name whitespace service_address? value? comment_text? line_break

frontend_header     <-      whitespace "frontend" whitespace proxy_name whitespace service_address? value? comment_text? line_break

backend_header      <-      whitespace "backend" whitespace proxy_name whitespace value? comment_text? line_break

config_block        <-      (server_line / option_line / bind_line / acl_line / backend_line / group_line / user_line / config_line / comment_line / blank_line)*

server_line         <-      whitespace "server" whitespace server_name whitespace service_address value? comment_text? line_break

option_line         <-      whitespace "option" whitespace keyword whitespace value? comment_text? line_break

bind_line           <-      whitespace "bind" whitespaceplus service_address whitespace value? comment_text? line_break

acl_line            <-      whitespace "acl" whitespace acl_name whitespace value? comment_text? line_break

backend_line        <-      whitespace ("use_backend" / "default_backend") whitespace backend_name whitespace ("if" / "unless")? whitespace backend_condition? comment_text? line_break

group_line          <-      whitespace "group" whitespace group_name whitespace ("users" whitespace)? value? comment_text? line_break

user_line           <-      whitespace "user" whitespace user_name whitespace ("password" / "insecure-password") whitespace password whitespace ("groups" whitespace)? value? comment_text? line_break

config_line         <-      whitespace !("defaults" / "global" / "userlist" / "listen" / "frontend" / "backend") keyword whitespace value? comment_text? line_break

comment_line        <-      whitespace comment_text line_break

blank_line          <-      whitespace line_break

comment_text        <-      "#" char* &line_break

line_break          <-      [\n]

keyword             <-      (("errorfile" / "timeout") whitespace)? [a-z0-9\-\_\.]+

server_name         <-      [a-zA-z0-9\-\_\.:]+

acl_name            <-      [a-zA-z0-9\-\_\.:]+

backend_name        <-      [a-zA-z0-9\-\_\.:]+

group_name          <-      [a-zA-z0-9\-\_\.:]+

user_name           <-      [a-zA-z0-9\-\_\.:]+

password            <-      [^#\n ]+

backend_condition   <-      [^#\n]+

service_address     <-      host [:]? port

host                <-      ipv4_host / dns_host / wildcard_host

port                <-      [\d]*

ipv4_host           <-      [\d]+ "." [\d]+ "." [\d]+ "." [\d]+

dns_host            <-      [a-zA-Z\-\.\d]+

wildcard_host       <-      "*"

proxy_name          <-      [a-zA-Z0-9\-\_\.:]+

value               <-      [^#\n]+

char                <-      ![\n] .

whitespace          <-      [ \t]*

whitespaceplus      <-      [ \t]+
